// Type for return
type MyReturn = MyReturn{c: Real[], k: Real[], theta: Real[]}

/*
 * Recursive function per sample
 */
function process_samples(dataset: Int[][], k: Real[], theta: Real[], mu: Real, sigma: Real, c_seq: Real[], j: Int) => Real[] { 
  assume logC ~ Gaussian(mu, sigma); 
  let c = exp(logC);
  let c_seq = paste0([c_seq,[c]]);
  for i in 1 to 2 { //i is index of spike-in species(artificial or biological) or simply species index
    if (dataset[j][i] != 0) { //cannot observe a zero from Gamma
      observe Real(dataset[j][i]) ~ Gamma(k[i], c * theta[i]);
    }
  }
  resample;
  if (j == 15) { //15 samples
    return c_seq;
  }
  else {
    return process_samples(dataset, k, theta, mu, sigma, c_seq, (j+1));
  }
}

/*
 * Model function
 * Data:
 *   Homogenate data, 15 samples. dataset is number of reads r per species per sample for two artificial spikeins
 */
model function myModel(art_spikeins: Int[][], k_1: Real, k_2: Real, theta_1: Real, theta_2: Real, tau_1: Real, tau_2: Real, mu_1: Real) => MyReturn  {
  let k = iid(gamma, GammaParam{shape = k_1, scale = k_2}, 2); 
  let logTheta = iid(gaussian, GaussianParam{mean = theta_1, dev = theta_2}, 2); 
  let theta = sapply(logTheta, exp); 
  
  assume tau ~  Gamma(tau_1, tau_2);  
  let sigma = sqrt(1.0 / tau); 
  assume mu ~ Gaussian(mu_1, sigma);  

  let c_seq = process_samples(art_spikeins, k, theta, mu, sigma, [], 1);

  return MyReturn{c = c_seq, k = k, theta = theta}; 
}