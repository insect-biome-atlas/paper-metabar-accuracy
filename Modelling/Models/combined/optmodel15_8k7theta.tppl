/*
 * File: optmodel15_8k8theta.tppl
 * Description: Probabilistic model taken from optimal metabarcoding adjusted to 15samples
 * Compilation:
 *  tpplc models/metabarcoding/optmodel15_8k8theta.tppl -m 'smc-apf' --resample align --subsample -n 1 --output optmodel15_8k8theta
 * Execution with 10000 particles and 1 sweeps:
 * ./optmodel15_8k8theta models/data/15samples/15_hom_combined.json 10000 1 > output/output_april/15s_8k8theta.json
 */

// Type for return
type MyReturn = MyReturn{c: Real[], k: Real[], theta: Real[]}

/*
 * Recursive function per sample
 */
function process_samples(art_spikeins: Int[][], bio_spikeins: Int[][], k: Real[], theta: Real[], mu: Real, sigma: Real, c_seq: Real[], j: Int, n: Int[]) => Real[] { 
  assume logC ~ Gaussian(mu, sigma); 
  let c = exp(logC);
  let c_seq = paste0([c_seq,[c]]);
    //Observe art and bio spikeins
    observe Real(art_spikeins[j][1]) ~ Gamma(k[1], c * theta[1]);
    observe Real(art_spikeins[j][2]) ~ Gamma(k[2], c * theta[1]);
    for i in 1 to 6 { // 6 bioloical spikeins.
      if (bio_spikeins[j][i] != 0) { //cannot observe a zero from Gamma
        observe Real(bio_spikeins[j][i]) ~ Gamma(Real(n[i]) * k[(2+i)], c * theta[(1+i)]);
      }
    }
  resample;
  if (j == 15) { //15 samples
    return c_seq;
  }
  else {
    return process_samples(art_spikeins, bio_spikeins, k, theta, mu, sigma, c_seq, (j+1), n);
  }
}

/*
 * Model function
 * Data:
 *   Homogenate data, 15 samples. dataset is number of reads r per species per sample for six biological spikeins and two artificial spikeins.
 *
 */
model function myModel(art_spikeins: Int[][], bio_spikeins: Int[][], k_1: Real, k_2: Real, theta_1: Real, theta_2: Real, tau_1: Real, tau_2: Real, mu_1: Real, n_spikeins: Int[]) => MyReturn  {
  let k = iid(gamma, GammaParam{shape = k_1, scale = k_2}, 8); 
  let logTheta = iid(gaussian, GaussianParam{mean = theta_1, dev = theta_2}, 7); 
  let theta = sapply(logTheta, exp); 
  
  assume tau ~  Gamma(tau_1, tau_2);  
  let sigma = sqrt(1.0 / tau); 
  assume mu ~ Gaussian(mu_1, sigma);  

  let c_seq = process_samples(art_spikeins, bio_spikeins, k, theta, mu, sigma, [], 1, n_spikeins);

  return MyReturn{c = c_seq, k = k, theta = theta}; 
}